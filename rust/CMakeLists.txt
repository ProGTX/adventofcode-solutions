set(RUST_DIR "${CMAKE_CURRENT_SOURCE_DIR}")
set(RUST_TARGET_DIR "${RUST_DIR}/target")
set(RUST_BUILD_DIR_EXPR "${RUST_TARGET_DIR}/$<IF:${AOC_RELEASE_CHECK_EXPR},release,debug>")
set(RUST_BUILD_STAMP "${RUST_BUILD_DIR_EXPR}/.stamp")
set(RUST_GENERATED "Generated by CMake, do not modify manually")
define_property(GLOBAL PROPERTY RUST_SOURCES BRIEF_DOCS "List of all Rust source files")

# Create a dummy target that the tests can depend on
# Tests cannot depend on this stamp directly (for some CMake reason)
add_custom_target(rust_build ALL
    DEPENDS ${RUST_BUILD_STAMP}
)

function(add_rust_test sample_name year)
    # TODO: Use add_rust_source
    set_property(GLOBAL APPEND PROPERTY RUST_SOURCES
        "${RUST_DIR}/${year}/src/${sample_name}.rs"
    )

    add_test(NAME ${year}_${sample_name}_rust
        COMMAND ${RUST_BUILD_DIR_EXPR}/${year}_${sample_name}
        DEPENDS rust_build
        WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/${year}"
    )

    # rust_local_sources is needed by generate_cargo_toml
    set(local_sources "${rust_local_sources}")
    list(APPEND local_sources "${sample_name}")
    set(rust_local_sources "${local_sources}" PARENT_SCOPE)
endfunction()

# Generate Cargo.toml for the given year
# after all tests have been added for that year
function(generate_cargo_toml year rust_local_sources)
    set(YEAR ${year})
    set(CARGO_BIN_SECTIONS "")
    foreach(src ${rust_local_sources})
        string(APPEND CARGO_BIN_SECTIONS
"[[bin]]
name = \"${year}_${src}\"
path = \"src/${src}.rs\"
")
    endforeach()
    configure_file(
        "${RUST_DIR}/Cargo.toml.in"
        "${RUST_DIR}/${year}/Cargo.toml"
        @ONLY
    )
endfunction()

add_subdirectory(utils)
add_subdirectory(2015)
add_subdirectory(2021)
add_subdirectory(2022)

# After all tests have been added for all years,
# create the command that actually builds all the Rust source files
# Cargo takes care of building everything,
# CMake just needs to know about dependencies
get_property(rust_sources GLOBAL PROPERTY RUST_SOURCES)
add_custom_command(
    OUTPUT ${RUST_BUILD_STAMP}
    COMMAND "${CARGO_EXECUTABLE}" build
        $<${AOC_RELEASE_CHECK_EXPR}:--release>
    COMMAND ${CMAKE_COMMAND} -E touch ${RUST_BUILD_STAMP}
    WORKING_DIRECTORY "${RUST_DIR}"
    DEPENDS "${RUST_DIR}/Cargo.toml" "${RUST_DIR}/utils/Cargo.toml" "${rust_sources}" 
    COMMENT "Building all Rust workspaces"
)
